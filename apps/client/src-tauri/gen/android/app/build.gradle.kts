import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("rust")
}

val tauriProperties = Properties().apply {
    val propFile = file("tauri.properties")
    if (propFile.exists()) {
        propFile.inputStream().use { load(it) }
    }
}

/*
 * The key releases are signed with. It lives outside the repository, in the
 * home folder of the machine that builds releases, and must be kept: Android
 * installs an update only over an app signed with the same key, so a lost key
 * means everyone reinstalling. Without it, a release build is left unsigned.
 * BASALT_SIGNING points somewhere else if the key is kept elsewhere.
 */
val signing = Properties().apply {
    val where = System.getenv("BASALT_SIGNING")
        ?: "${System.getProperty("user.home")}/.basalt/android-signing/keystore.properties"
    val propFile = file(where)
    if (propFile.exists()) {
        propFile.inputStream().use { load(it) }
    }
}

android {
    compileSdk = 36
    namespace = "app.basalt.client"
    defaultConfig {
        manifestPlaceholders["usesCleartextTraffic"] = "false"
        // The name Android installs the app under, and the one Google Play will
        // keep for good. The code itself still lives in app.basalt.client,
        // which is Tauri's identifier and also the Windows apps'; changing
        // that would make Windows install Basalt a second time beside itself.
        applicationId = "com.reforatech.basalt"
        minSdk = 26
        targetSdk = 36
        versionCode = tauriProperties.getProperty("tauri.android.versionCode", "1").toInt()
        versionName = tauriProperties.getProperty("tauri.android.versionName", "1.0")
    }
    signingConfigs {
        if (signing.getProperty("storeFile") != null) {
            create("release") {
                storeFile = file(signing.getProperty("storeFile"))
                storePassword = signing.getProperty("storePassword")
                keyAlias = signing.getProperty("keyAlias")
                keyPassword = signing.getProperty("keyPassword")
            }
        }
    }
    buildTypes {
        getByName("debug") {
            manifestPlaceholders["usesCleartextTraffic"] = "true"
            isDebuggable = true
            isJniDebuggable = true
            isMinifyEnabled = false
            packaging {                jniLibs.keepDebugSymbols.add("*/arm64-v8a/*.so")
                jniLibs.keepDebugSymbols.add("*/armeabi-v7a/*.so")
                jniLibs.keepDebugSymbols.add("*/x86/*.so")
                jniLibs.keepDebugSymbols.add("*/x86_64/*.so")
            }
        }
        getByName("release") {
            signingConfigs.findByName("release")?.let { signingConfig = it }
            // The Google Play build (BASALT_CHANNEL=play) leaves out the
            // permission the GitHub build installs its own updates with.
            if (System.getenv("BASALT_CHANNEL") == "play") {
                sourceSets.getByName("release").manifest.srcFile("src/play/AndroidManifest.xml")
            }
            // 64-bit ARM, which is every phone of the last several years. The
            // player's libraries come for four kinds of processor, and the
            // others would only make the download larger for nobody.
            ndk {
                abiFilters.clear()
                abiFilters.add("arm64-v8a")
            }
            isMinifyEnabled = true
            proguardFiles(
                *fileTree(".") { include("**/*.pro") }
                    .plus(getDefaultProguardFile("proguard-android-optimize.txt"))
                    .toList().toTypedArray()
            )
        }
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        buildConfig = true
    }
}

rust {
    rootDirRel = "../../../"
}

dependencies {
    implementation("androidx.webkit:webkit:1.14.0")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("androidx.activity:activity-ktx:1.10.1")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.lifecycle:lifecycle-process:2.10.0")
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.4")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.0")
}

apply(from = "tauri.build.gradle.kts")